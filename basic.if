## <summary>Policy for user domains</summary>

#######################################
## <summary>
##	The template containing the most basic rules common to all users.
## </summary>
## <desc>
##	<p>
##	The template containing the most basic rules common to all users.
##	</p>
##	<p>
##	This template creates a user domain, types, and
##	rules for the user's tty and pty.
##	</p>
## </desc>
## <param name="userdomain_prefix">
##	<summary>
##	The prefix of the user domain (e.g., basic
##	is the prefix for basic_t).
##	</summary>
## </param>
## <rolebase/>
#
template(`confinedom_base_user_login_template',`

	gen_require(`
		attribute userdomain, login_confinedom;
		type user_devpts_t, user_tty_device_t;
		class context contains;
	')

	type $1_t;
	typeattribute $1_t login_confinedom;
	typeattribute $1_t userdomain;
	corecmd_shell_entry_type($1_t)
	domain_type($1_t)
	domain_user_exemption_target($1_t)
	ubac_constrained($1_t)
	role $1_r;
	role $1_r types $1_t;
	allow system_r $1_r;

	term_use_all_terms($1_t)

	allow $1_t $1_t:capability { setgid setuid };
	allow $1_t $1_t:process { setcap setfscreate setsched setsockcreate };

	allow $1_t $1_t:netlink_kobject_uevent_socket { bind create getattr getopt setopt };
	allow $1_t $1_t:unix_dgram_socket { bind connect create getopt ioctl setopt };
	allow $1_t $1_t:unix_stream_socket connectto;

	application_exec_all($1_t)
	
	auth_read_passwd($1_t)

	files_dontaudit_manage_boot_dirs($1_t)
	files_dontaudit_manage_boot_files($1_t)		

	fs_getattr_cgroup($1_t)
	fs_getattr_all_dirs($1_t)
	fs_getattr_tmpfs($1_t)
	fs_getattr_xattr_fs($1_t)
	fs_manage_cgroup_dirs($1_t)
	fs_manage_cgroup_files($1_t)
	fs_read_tmpfs_files($1_t)

	init_entrypoint_exec($1_t)
	init_exec($1_t)

	kernel_dgram_send($1_t)
	kernel_read_all_sysctls($1_t)
	kernel_read_network_state($1_t)
	kernel_stream_connect($1_t)

	logging_send_syslog_msg($1_t)

	miscfiles_watch_localization_symlinks($1_t)

	mount_read_pid_files($1_t)
	mount_watch_pid_dirs($1_t)
	
	dbus_send_system_bus($1_t)

	dev_getattr_sound_dev($1_t)
	dev_read_sysfs($1_t)

	selinux_compute_access_vector($1_t)
	selinux_compute_create_context($1_t)
	selinux_get_enforce_mode($1_t)

	seutil_read_file_contexts($1_t)
	
	storage_getattr_fixed_disk_dev($1_t)
		
	systemd_dbus_chat_hostnamed($1_t)
	systemd_exec_systemctl($1_t)
	systemd_tmpfiles_exec($1_t)
	systemd_read_unit_files($1_t)

	udev_read_pid_files($1_t)

#       constraint violation issue
#	userdom_manage_user_tmp_symlinks($1_t)
#	userdom_manage_all_user_tmp_content(login_confinedom)
#	userdom_manage_user_home_content_dirs($1_t)
	userdom_manage_user_home_content_files($1_t)
#	userdom_manage_user_tmp_chr_files(login_confinedom)

')

########################################
## <summary>
##      Allow caller to transition to confined userdomain.
## </summary>
## <param name="domain">
##      <summary>
##      Domain which need allow access.
##      </summary>
## </param>
## <param name="domain">
##      <summary>
##      Domain allowed access.
##      </summary>
## </param>
#
interface(`confined_transition_userdomain',`
      gen_require(`
                attribute login_confinedom;
        ')        
	allow $1 login_confinedom:process transition;
')

########################################
## <summary>
##      Allow caller to use basic command for confined user.
## </summary>
## <param name="domain">
##      <summary>
##      Domain which need allow access.
##      </summary>
## </param>
## <param name="domain">
##      <summary>
##      Domain allowed access.
##      </summary>
## </param>
#
interface(`confined_use_basic_commands',`
      gen_require(`
#replace in 9.1 init_mmap_read_var_lib_files()
                type init_var_lib_t;
		attribute login_confinedom;
        ')

	allow $1_t $1_t:process setpgid;

# replace then
	allow $1_t init_var_lib_t:file map;
	init_read_var_lib_files(login_confinedom)
	init_signal($1_t)

	logging_read_generic_logs($1_t)
	logging_read_syslog_pid($1_t)

	systemd_status_all_unit_files($1_t)		
')

######################################
## <summary>
##  The template allow ssh connection.
## </summary>
## <param name="domain">
##  <summary>
##  Domain allowed access.
##  </summary>
## </param>
#
template(`confined_ssh_connect_template',`
	
	ssh_dyntransition_to($1_t)
	term_use_ptmx($1_t)
')
